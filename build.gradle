apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.sonarqube'
apply plugin: 'maven-publish'
apply plugin: "com.jfrog.artifactory"
apply plugin: "com.github.hierynomus.license"
apply plugin: 'signing'
apply plugin: 'maven'
//apply plugin: 'nebula.lint'

sourceCompatibility = 1.8
version = "${version}"
group = 'com.dell.isg.smi'
archivesBaseName = "wsmanclient"
ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

sourceSets {
    main {
        java {
            srcDir 'src/main/generated'
        }
    }
}

buildscript {
   repositories {
    mavenLocal()
	maven {
		url "${artifactory_contextUrl}/${plugin_repo}"
		url "https://plugins.gradle.org/m2/"
	}
  }
  dependencies {
  	classpath "io.spring.gradle:dependency-management-plugin:1.0.0.RELEASE"
    classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4.4.0"
    classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.1"
    classpath "gradle.plugin.nl.javadude.gradle.plugins:license-gradle-plugin:0.13.1"
    //classpath 'com.netflix.nebula:gradle-lint-plugin:latest.release'
  }
}

if (JavaVersion.current().isJava8Compatible()) {
    tasks.withType(Javadoc) {
        options.addStringOption('Xdoclint:none', '-quiet')
    }
}

javadoc {
    options {
         encoding = 'windows-1251'
    }
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

artifacts {
    archives javadocJar, sourcesJar
}

//gradleLint {
  //rules  = ['dependency-parentheses','overridden-dependency-version','duplicate-dependency-class', 'all-dependency','unused-dependency']
//}

sonarqube {
    properties {
        property "sonar.projectName", "SMI :: Library :: Wsmanclient"
        property "sonar.projectKey", "org.sonarqube:wsmanclient"
    }
}

jar {
    manifest {
        attributes 'Implementation-Title': 'wsmanclient jar',
                   'Implementation-Version': version
    }
}

repositories {
	mavenLocal()
	maven {
		url "${artifactory_contextUrl}/${dependency_repo}"
	}
}

dependencies {
	compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'
    compile 'com.dell.isg.smi:commons-elm:1.0.76'
	compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.5'
	compile group: 'xerces', name: 'xercesImpl', version: '2.11.0'
}

test {
    systemProperties 'property': 'value'
}

uploadArchives {
    repositories {
       flatDir {
           dirs 'repos'
       }
       
         mavenDeployer
	   {
			beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
			
			repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
				authentication(userName: ossrhUsername, password: ossrhPassword)
			}

			snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
				authentication(userName: ossrhUsername, password: ossrhPassword)
			}
	  
	        pom.project {
				artifactId = "wsmanclient"
				packaging 'jar'
				url 'https://www.github.com/rackhd/smi-lib-wsman-client'
				name 'SMI Commons ELM'
				description 'WSMAN Client jar library for the System Management Integration (SMI) projects.'
				inceptionYear '2017'
				
				licenses {
					license {
						name 'The Apache Software License, Version 2.0'
						url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
						distribution 'repo'
					}
				}
				
				scm {
				  connection 'scm:git:git://www.github.com/rackhd/smi-lib-wsman-client.git'
				  developerConnection 'scm:git:ssh://www.github.com/rackhd/smi-lib-wsman-client.git'
				  url 'http://www.github.com/rackhd/smi-lib-wsman-client'
				}
				
				developers {
					developer {
						id 'michaelsteven'
						name 'Michael Hepfer'
						email 'michael.hepfer@dell.com'
						organization 'Dell EMC Inc.'
						organizationUrl 'http://codedellemc.com'
						}
				}
				
			}	   
	    }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication){
            artifact jar
            artifact sourcesJar {
                classifier "sources"
            }
            artifact javadocJar
        }
    }
}

license {
        header rootProject.file('license-template')
        includes(["**/*.java", "**/*.properties"])
        strictCheck true
        ignoreFailures true
        ext.year = Calendar.getInstance().get(Calendar.YEAR)
    	ext.name = 'DELL Inc.'
}

artifactory {
    contextUrl = "${artifactory_contextUrl}"   //The base Artifactory URL if not overridden by the publisher/resolver
    publish {
        defaults{ publications ("mavenJava")}
        repository {
	    // note: username and password being set by bamboo artifactory plugin
            maven = true
        }
    }
    resolve {
        repository {
            repoKey = 'libs-release'
            maven = true
        }
    }
}
